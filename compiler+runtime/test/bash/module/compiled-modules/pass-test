#!/usr/bin/env bb

^{:clj-kondo/ignore [:namespace-name-mismatch]}
(ns jank.test.module.compiled-module
  (:require [babashka.fs :as fs]
            [babashka.process :as proc]
            [clojure.string :as string]
            [clojure.test :as t :refer [deftest is testing use-fixtures]]))

(def this-nsym (ns-name *ns*))

(def module-a "jank-test.a")
(def module-a-path "./src/jank_test/a.cljc")
(defn compiled-a-path []
  (first (fs/glob "./target" "**/a.o")))

(defn jank [command module-name & {:keys [extra-module-path]}]
  (proc/sh ["jank" "--module-path"
            (str "src" (when-not (empty? extra-module-path)
                         (str ":" extra-module-path)))
            command module-name]))

(defn create-jar-with-a []
  (let [jar-name "test.jar"
        [compilation-dir rest-of-module-path] (->> (fs/glob "target" "**/a.o")
                                                   (first)
                                                   (fs/components)
                                                   (split-at 2)
                                                   (map (partial apply fs/path))
                                                   (map str))]
    (proc/sh {:dir compilation-dir} "jar" "cf" jar-name rest-of-module-path)
    (fs/path compilation-dir jar-name)))

(defmacro with-hidden-file
  "Temporarily renames `path` to `path.bak`, evaluates body, 
   and restores the file in a `finally` block."
  [path & body]
  `(let [path# ~path
         bak# (str path# ".bak")]
     (fs/move path# bak#)
     (try
       ~@body
       (finally
         (fs/move bak# path#)))))

(use-fixtures
  :each
  (fn [f]
    (jank "compile-module" module-a)
    (f)
    (fs/delete-tree "./target")))

(deftest compiled-module-gets-loaded
  (testing "loads a compiled module instead of the source"
    (is (false? (-> (jank "run-main" module-a)
                    :out
                    (string/includes? ":only-during-read")))))

  (testing "Source fresher than the compiled module, loads from source"
    (proc/sh ["touch" module-a-path])
    (-> (jank "run-module" module-a)
        :out
        println)
    (is (true? (-> (jank "run-main" module-a)
                   :out
                   (string/includes? ":only-during-read"))))))

(deftest compiled-module-loadability
  (testing "Doesn't load a module from archive"
    (fs/with-temp-dir [tmp-dir]
      (let [jar (-> (create-jar-with-a)
                    (fs/move tmp-dir))]
        (with-hidden-file "target"
          (is (true? (-> (jank "run-main" module-a :extra-module-path jar)
                         :out
                         (string/includes? ":only-during-read"))))))))

  (testing "Doesn't load module if source not found"
    (with-hidden-file module-a-path
      (is (true? (-> (jank "run-main" module-a)
                     :out
                     (string/includes? "No sources for registered module")))))))

(defn -main []
  (System/exit
   (if (t/successful? (t/run-tests this-nsym))
     0
     1)))

(when (= *file* (System/getProperty "babashka.file"))
  (apply -main *command-line-args*))
