#!/usr/bin/env bb

(ns jank.test.cli
  (:require [babashka.process :as proc]
            [clojure.string]
            [clojure.test :as t :refer [deftest is]]))

(def this-nsym (ns-name *ns*))

(defn assert-exit-code [expected-code & cmd]
  (let [{:keys [exit] :as res} (apply proc/shell {:out :string
                                                  :err :string
                                                  :continue true}
                                      cmd)
        res (update res :out clojure.string/trim)]
    (is (= expected-code exit)
        (pr-str (select-keys res [:exit :out :cmd])))
    res))

(deftest jank-exit-code-test
  (assert-exit-code 0 "jank --module-path src run-main jank-test.successful-exit-code")
  (assert-exit-code 1 "jank --module-path src run-main jank-test.fail-throw-exit")
  (assert-exit-code 1 "jank --module-path src run-main jank-test.fail-uncatchable-exit")
  (assert-exit-code 1 "jank")
  (assert-exit-code 1 "jank run src/jank_test/throwing_script.jank")
  (assert-exit-code 1 "jank run src/jank_test/uncatchable_script.jank")
  (assert-exit-code 0 "jank run src/jank_test/successful_script.jank"))

(deftest jank-command-line-args-test
  (let [res (assert-exit-code 0 "jank run src/jank_test/print_args.jank -- 1 2 3")]
    (is (= "(1 2 3)" (:out res)))))

(defn -main []
  (System/exit
    (if (t/successful? (t/run-tests this-nsym))
      0
      1)))

(when (= *file* (System/getProperty "babashka.file"))
  (apply -main *command-line-args*))
