#!/usr/bin/env bb

(ns jank.test.direct-call
  (:require [babashka.fs :as fs]
            [babashka.process :as proc]
            [clojure.test :as t :refer [deftest is use-fixtures]]))

(def this-nsym (ns-name *ns*))

(def test-dir (str (fs/canonicalize (fs/parent *file*))))
(def module-path (str test-dir "/src"))
(def run-file (str module-path "/jank_test/direct_call/run.jank"))
(def expected-output "42\n")
(def base-cmd (str "jank --module-path " module-path " --direct-call --codegen cpp"))

(def redefs-run-file (str module-path "/jank_test/direct_call/redefs_run.jank"))

(defn sh! [cmd]
  (let [{:keys [exit out err] :as res}
        (proc/shell {:out :string :err :string :continue true} cmd)]
    (is (= 0 exit) (pr-str (select-keys res [:cmd :exit :out :err])))
    out))

(defn object-paths [module]
  (let [suffix (str "jank_test/direct_call/" module ".o")]
    (concat (fs/glob "." (str "target/" suffix))
            (fs/glob "." (str "target/**/" suffix))
            (fs/glob "." (str "build/target/" suffix))
            (fs/glob "." (str "build/target/**/" suffix)))))

(use-fixtures
  :each
  (fn [f]
    (doseq [dir ["target" "build/target"]]
      (when (fs/exists? dir)
        (fs/delete-tree dir)))
    (println "Cleaned up module caches")
    (f)))

(deftest run-with-source-dependency
  (is (= expected-output (sh! (str base-cmd " run " run-file)))))

(deftest run-do-case-top-level-do
  (let [do-case-file (str module-path "/jank_test/direct_call/do_case.jank")
        do-case-output "Foo\n"]
    (is (= do-case-output (sh! (str base-cmd " run " do-case-file))))))

(deftest run-with-compiled-dependency
  (sh! (str base-cmd " compile-module jank-test.direct-call.util"))
  (is (seq (object-paths "util")))
  (is (= expected-output (sh! (str base-cmd " run " run-file)))))

(deftest compile-module-with-source-dependency
  (sh! (str base-cmd " compile-module jank-test.direct-call.core"))
  (is (seq (object-paths "core")))
  (is (seq (object-paths "util")))
  (is (= expected-output (sh! (str base-cmd " run " run-file)))))

(deftest compile-module-with-compiled-dependency
  (sh! (str base-cmd " compile-module jank-test.direct-call.util"))
  (sh! (str base-cmd " compile-module jank-test.direct-call.core"))
  (is (seq (object-paths "core")))
  (is (seq (object-paths "util")))
  (is (= expected-output (sh! (str base-cmd " run " run-file)))))

(deftest with-redefs-direct-call
  (let [redefs-direct-call-output "1\n1\n"]
    (is (= redefs-direct-call-output (sh! (str base-cmd " run " redefs-run-file))))))

(deftest with-redefs-control
  (let [base-cmd-no-direct-call (str "jank --module-path " module-path " --codegen cpp")
        redefs-control-output "1\n999\n"]
    (is (= redefs-control-output
           (sh! (str base-cmd-no-direct-call " run " redefs-run-file))))))

(defn -main []
  (System/exit
    (if (t/successful? (t/run-tests this-nsym))
      0
      1)))

(when (= *file* (System/getProperty "babashka.file"))
  (apply -main *command-line-args*))
