(ns jank.nrepl.server.handler.lookup
  (:require [jank.nrepl.server.handler :refer [handle-message]]))

(defn lookup
  "Lookup meta for the a symbol defined in a namespace, or return nil if either
  is not defined."
  [ns sym]
  (let [ns'  (some-> ns symbol find-ns)
        sym' (some-> sym symbol)]
    (when (and ns' sym')
      (some-> (ns-resolve ns' sym') meta))))

(defmethod handle-message :lookup [msg]
  (let [ns  (get msg "ns")
        sym (get msg "sym")]
    (if-let [m (lookup ns sym)]
      {:info   {:ns       ns
                :name     sym
                :arglists (str (:arglists m))
                :doc      (str (:doc m))}
       :status [:done]}
      {:status [:error :done]})))
